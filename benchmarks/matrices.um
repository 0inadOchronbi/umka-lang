// Matrix multiplication benchmark - Umka version

import "../import/std.um"

fn main() {
    const size = 400
    var a, b, c: [size][size] real
    
    // Fill matrices
    for i, row in a {
        for j, col in row {
            a[i][j] = 3 * i + j
            b[i][j] = i - 3 * j
        }
    }
    
    // Multiply matrices
    start := std.time()
    
    for i := 0; i < size; i++ {
        for j := 0; j < size; j++ {
            s := 0.0
            for k := 0; k < size; k++ {s += a[i][k] * b[k][j]}
            c[i][j] = s
        }
    }    
    
    printf("elapsed: %d\n", std.time() - start)
    
    // Check result
    check := 0.0
    for row in c {
        for col in row {check += col}
    }

    printf("check: %lf\n", check / size / size)
}